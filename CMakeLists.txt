cmake_minimum_required(VERSION 3.10.0)
if(POLICY CMP0110)
    cmake_policy(SET CMP0110 OLD)
endif()
project(CalcMySky VERSION 0.0.1 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
if(${CMAKE_CXX_COMPILER_ID} MATCHES "GNU|Clang")
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Werror=return-type -Wall -Wextra -fvisibility=hidden")
endif()

if(MSVC)
    set(CMAKE_CXX_FLAGS "/wd4267 /wd4305 /wd4244 ${CMAKE_CXX_FLAGS}")
endif()
if(ADD_CXX_FLAGS)
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${ADD_CXX_FLAGS}")
endif()
include(cmake/CheckGLM.cmake)

set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTOUIC ON)
find_package(Qt5 5.9 REQUIRED Core OpenGL)
find_package(Eigen3 REQUIRED)
include_directories(SYSTEM ${EIGEN3_INCLUDE_DIR})
include_directories(${CMAKE_BINARY_DIR})

if(WIN32)
    set(shadersDestPath "${CMAKE_INSTALL_PREFIX}/${CMAKE_PROJECT_NAME}")
    set(installBinDir "${CMAKE_INSTALL_PREFIX}/${CMAKE_PROJECT_NAME}")
    set(installLibDir "${installBinDir}")
    set(installConfDir "${CMAKE_INSTALL_PREFIX}/${CMAKE_PROJECT_NAME}/lib")
    set(installIncDir "${CMAKE_INSTALL_PREFIX}/${CMAKE_PROJECT_NAME}/include")

    # Deploy Qt
    foreach(lib Core Widgets Gui)
        install(FILES "$<TARGET_FILE:Qt5::${lib}>" DESTINATION "${installBinDir}")
    endforeach()
    install(FILES "${Qt5Core_DIR}/../../../plugins/platforms/qwindows$<$<CONFIG:Debug>:d>.dll" DESTINATION "${installBinDir}/platforms")

else()
    include(GNUInstallDirs)
    set(shadersDestPath "${CMAKE_INSTALL_DATAROOTDIR}/${CMAKE_PROJECT_NAME}")
    set(installBinDir "${CMAKE_INSTALL_BINDIR}")
    set(installLibDir "${CMAKE_INSTALL_LIBDIR}")
    set(installConfDir "${CMAKE_INSTALL_LIBDIR}")
    set(installIncDir "${CMAKE_INSTALL_INCLUDEDIR}")
endif()

install(DIRECTORY "${CMAKE_SOURCE_DIR}/shaders" DESTINATION "${shadersDestPath}")

configure_file(config.h.in config.h)
add_subdirectory(CalcMySky)
add_subdirectory(ShowMySky)

enable_testing()
add_subdirectory(tests)
